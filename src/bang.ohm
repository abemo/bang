Bang {
  // remove "\n" from space
  space := "\n" | " " | "\t" | "\r" | multiLineComment | comment
  /*
  program = block
  block = "\n"* (statement "\n"+)* statement "\n"*
  */
  Program               = Statement*
  BangFunc              = "{" LocalStatement* "}"                           -- multiLine
                        | Statement
  // TODO statements can be stuck together on one line ex. `float = 5 str = "test"`
  LocalStatement        = local VarSelect (assignmentOp Exp)?               -- localVar
                        | local const VarSelect assignmentOp Exp            -- localConst
                        | return Exp?                                       -- return
                        | break
                        | Statement
  Statement             = const? VarSelect assignmentOp Exp                 -- varAssignment
                        | print "(" Exp ")"                                 -- print
                        | Exp
  Exp                   = FuncLit
                        | Exp (("=" | "!") "=" | ("<" | ">") "="?) Exp1     -- equality
                        | "(" Exp1 ")"                                      -- enclosed
                        | Ternary
                        | Exp1
  Exp1                  = Exp1 "||" Exp2                                    -- or
                        | Exp2
  Exp2                  = Exp2 "&&" Exp3                                    -- and
                        | Exp3
  Exp3                  = "!" Exp4                                          -- not
                        | Exp4
  Exp4                  = Exp4 ("+" | "-") Exp5                             -- addSubtract
                        | Exp5
  Exp5                  = Exp5 ("*" | "/" | "%") Exp6                       -- multiplyDivideMod
                        | Exp6
  Exp6                  = Exp7 "**" Exp6                                    -- exponent
                        | "-"? Exp7                                         -- negative
  Exp7                  = (VarSelect | id) Params?                          -- call
                        | Exp8
  Exp8                  = nil       | Str       | Num       | Obj       
                        | boolLit   | BangFunc  | Enum      | MatchExp
                        | ListLit   | Range     
  VarSelect             = (Exp8 | id) "[" Exp "]"                           -- subscript
                        | (Exp8 | id)? "." id                               -- select
                        | id

  // ternaries
  // todo Exp lineTerminator* "?" BangFunc (lineTerminator* ":" BangFunc)?
  Ternary               = Exp "?" BangFunc (":" BangFunc)?
  
  // function stuff
  FuncLit               = Params arrow BangFunc
  Params                = "(" ListOf<Arg, ","> ")"
  Arg                   = PositionalArg | KeywordArg
  PositionalArg         = Exp
  KeywordArg            = id "=" Exp
  arrow                 = "->"
  print                 = "print" // TODO: test for `x = 5`//`printx(x)`

  // objects
  Obj                   = "{" ObjField* "}"
  ObjField              = key ":" Exp
  key                   = strLit

  // lists
  ListLit               = "[" ListOf<Exp, ","> "]"

  // range
  Range                 = range "("( digit+ ",")? digit+ ")"
  range                 = "range"

  // strings
  Str                   = strLit | FormattedStr
  strLit                = "'" singleStrChar* "'"
                        | "\"" doubleStrChar* "\""
  FormattedStr          = "$'" FSingleSubstr* "'"                           -- singleQuotes
                        | "$\"" FDoubleSubstr* "\""                         -- doubleQuotes
  FSingleSubstr         = fSingleStrChar | FStrExp
  FDoubleSubstr         = fDoubleStrChar | FStrExp
  FStrExp               = "{" Exp "}"
  
  // special chars
  fSingleStrChar        = ~"{" singleStrChar
  fDoubleStrChar        = ~"{" doubleStrChar
  singleStrChar         = ~("'" | "\\" | lineTerminator) any                -- nonEscaped
                        | "\\" escapeChar                                   -- escaped
                        | lineContinuation
  doubleStrChar         = ~("\"" | "\\" | lineTerminator) any               -- nonEscaped
                        | "\\" escapeChar                                   -- escaped
                        | lineContinuation
  escapeChar            = "'" | "\"" | "\\" | "n" | "t" | "r" | "u"
  char                  = ~(escapeChar | lineTerminator) any
  lineTerminator        = "\n" | "\r" ~"\n" | "\u2028" | "\u2029"
  lineContinuation      = "\\" lineTerminator

  // variables
  const                 = "const" ~idchar
  id                    = ~keyword (letter | "_") idchar* "?"?
  idchar                = alnum | "_"

  // booleans
  boolLit               = true | false
  true                  = "true" ~idchar
  false                 = "false" ~idchar

  // todo hex? "0x" hexNum
  // numbers
  Num                   = digit+ ("." digit+)? (("E" | "e") ("+" | "-")? digit+)?

  // TODO bitwise ops?
  // operators
  assignmentOp          = "=" | "+=" | "-=" | "*=" | "/=" | "%="

  // switch statements
  MatchExp              = match id MatchBlock
  MatchBlock             = "{" CaseClause+ DefaultClause CaseClause* "}"    -- withDefault
                        | "{" CaseClause+                           "}"     -- withoutDefault
  CaseClause            = case ListOf<Exp, ","> ":" BangFunc
  DefaultClause         = default ":" BangFunc
  match                 = "match" ~idchar
  case                  = "case" ~idchar
  default               = "default"

  // enums
  Enum                  = enum id "{" EnumBlock "}"
  EnumBlock             = EnumCaseAssignment+ | enumCases
  EnumCaseAssignment    = id "=" Exp
  enumCases             = id (("," | lineTerminator) spaces id)*
  enum                  = "enum" ~idchar

  return                = "return" ~idchar

  multiLineComment      = "/*" (~"*/" any)* "*/"
  comment               = "//" (~lineTerminator any)*

  // other
  keyword               = const     | print     | boolLit   | match    
                        | nil       | enum      | break     | return
                        | local     | range
  nil                   = "nil" ~idchar
  break                 = "break" ~idchar
  local                 = "local" ~idchar
}