Bang {
  space += multiLineComment | comment
  Program               = Block
  Block                 = #newLine* StatementNewLine* Statement? #newLine*  
  StatementNewLine      = Statement #(~newLine space)* #newLine+
  Statement             = local? const? id assignmentOp Exp                                 -- varDec
                        | local id                                                          -- localVar
                        | VarAssignment assignmentOp Exp                                    -- varAssignment
                        | return Exp?                                                       -- return
                        | Exp                                                               -- impliedReturn
                        | break
  Exp                   = Exp1 "?" (BangFunc | Statement) (":" (BangFunc | Statement))?     -- ternary
                        | Exp1
  Exp1                  = Exp2 compareOp NonemptyListOf<Exp2, compareOp>                    -- equality
                        | Exp2
  Exp2                  = Exp2 "||" Exp3                                                    -- or
                        | Exp3
  Exp3                  = Exp3 "&&" Exp4                                                    -- and
                        | Exp4
  Exp4                  = Exp5 additionOp NonemptyListOf<Exp5, additionOp>                  -- addSubtract
                        | Exp5
  Exp5                  = Exp6 multiplicationOp NonemptyListOf<Exp6, multiplicationOp>      -- multiplyDivideMod
                        | Exp6
  Exp6                  = Exp7 "**" Exp6                                                    -- exponent
                        | "-" Exp7                                                          -- negate
                        | "..." Exp7                                                        -- spread
                        | Exp7
  Exp7                  = Exp8 ("++" | "--")                                                -- postFix
                        | ("++" | "--") Exp8                                                -- preFix
                        | Exp8
  Exp8                  = Exp8 #(~newLine space)* ~#newLine Params                          -- call
                        | Exp8 #"[" Exp "]"                                                 -- subscript
                        | Exp8? #"." VarAssignment                                          -- select
                        | "!" Exp8                                                          -- negative
                        | Exp8 #"?"                                                         -- unwrap
                        | Exp9
  Exp9                  = nil       | Str       | num       | Obj       
                        | boolLit   | BangFunc  | MatchExp  | ListLit   
                        | FuncLit   | id
                        | "(" Exp ")"                                                       -- enclosed

  BangFunc              = "{" Block "}"
  VarAssignment         = VarAssignment #"[" Exp "]"                                        -- subscript
                        | VarAssignment #"." VarAssignment                                  -- select
                        | id
  
  FuncLit               = (Params | id) "->" (BangFunc | Statement)
  Params                = "(" ListOf<Arg, ","> ")"
  Arg                   = KeywordArg | PositionalArg
  PositionalArg         = Exp
  KeywordArg            = id "=" Exp

  Obj                   = "{" ListOf<ObjField, ","> "}"
  ObjField              = key ":" Exp
  key                   = strLit

  ListLit               = "[" ListOf<Exp, ","> "]"

  Str                   = strLit | FormattedStr
  strLit                = "'" singleStrChar* "'"
                        | "\"" doubleStrChar* "\""
  FormattedStr          = "$'" FSingleSubstr* "'"
                        | "$\"" FDoubleSubstr* "\""
  FSingleSubstr         = fSingleStrChar | FStrExp
  FDoubleSubstr         = fDoubleStrChar | FStrExp
  FStrExp               = "{" Exp "}"
  
  // special chars
  fSingleStrChar        = ~"{" singleStrChar
  fDoubleStrChar        = ~"{" doubleStrChar
  singleStrChar         = ~("'" | "\\" | newLine) any                                       -- nonEscaped
                        | "\\" escapeChar                                                   -- escaped
                        | lineContinuation
  doubleStrChar         = ~("\"" | "\\" | newLine) any                                      -- nonEscaped
                        | "\\" escapeChar                                                   -- escaped
                        | lineContinuation
  escapeChar            = "'" | "\"" | "\\" | "n" | "t" | "r" | "u"
  newLine               = "\n" | "\r" ~"\n" | "\u2028" | "\u2029"
  lineContinuation      = "\\" newLine

  const                 = "const" ~idchar
  id                    = ~keyword (letter | "_") idchar*
  idchar                = alnum | "_"

  boolLit               = true | false
  true                  = "true" ~idchar
  false                 = "false" ~idchar

  num                   = digit+ ("." digit+)? (("E" | "e") ("+" | "-")? digit+)?

  assignmentOp          = "=" | "+=" | "-=" | "*=" | "/=" | "%="
  compareOp             = (("=" | "!") "=") | ("<" | ">") "="?
  additionOp            = "+" | "-"
  multiplicationOp      = "*" | "/" | "%"

  MatchExp              = match id MatchBlock
  MatchBlock            = "{" CaseClause+ DefaultClause? "}"
  CaseClause            = case ListOf<Exp, ","> ":" (BangFunc | Statement)
  DefaultClause         = default ":" (BangFunc | Statement) 
  match                 = "match" ~idchar
  case                  = "case" ~idchar
  default               = "default"

  return                = "return" ~idchar

  multiLineComment      = "/*" (~"*/" any)* "*/"
  comment               = "//" (~newLine any)*

  keyword               = const     | boolLit   | match     | nil       
                        | break     | return    | local     
  nil                   = "nil" ~idchar
  break                 = "break" ~idchar
  local                 = "local" ~idchar
}